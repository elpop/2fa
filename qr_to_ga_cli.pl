#!/usr/bin/perl
#===================================================================#
# Program => qr_to_ga_cli.pl (In Perl 5.0)            version 0.0.1 #
#===================================================================#
# Autor         => Fernando "El Pop" Romo        (pop@cofradia.org) #
# Creation date => 01/September/2022                                #
#-------------------------------------------------------------------#
# Info => This program convert a snapshot of the Export accounts of #
#         the Google Authenticator App, Read the QR Code, Decode    #
#         Mime Base64 data, process with protoc (Google Protocol    #
#         Buffers Compiler) and make a conf file to use the program #
#         ga_cli.pl                                                 #
#-------------------------------------------------------------------#
# This code are released under the GPL 3.0 License. Any change must #
# be report to the authors                                          #
#                 (c) 2022 - Fernando Romo                          #
#===================================================================#
use strict;
use Image::Magick;
use Barcode::ZBar;
use MIME::Base64; 
use Google::ProtocolBuffers;

# Definition of the Protocol Buffers generated by Google Authenticator
# Export Accounts options
Google::ProtocolBuffers->parse("
     message GA {
         message Keys {
                required string pass   = 1;
                required string keyid  = 2;
                optional string issuer = 3;
                optional int32  one    = 4;
                optional int32  two    = 5;
                optional int32  three  = 6;
         }
         repeated Keys Index = 1;
         optional int32  start   = 2;
         optional int32  end     = 3;
         optional int32  current = 4;
     }",
     {create_accessors => 1}
);

# Work variables
my $data = '';
my %key_ring = ();
my @images = grep {/\.(jpg|jpeg|png)$/} @ARGV; # filter image files from command arguments 

# Process if exists the argument with a image filename 
if ($#images >=0) {
    # Process images
    foreach my $image (@images) {
        # Prepare to Read the QR using ZBar libs
        my $scanner = Barcode::ZBar::ImageScanner->new();
        $scanner->parse_config("enable");
    
        # Use ImageMagick to obtain image properties
        my $magick = Image::Magick->new();
        $magick->Read($image) && die;
        my $raw = $magick->ImageToBlob(magick => 'GRAY', depth => 8);
    
        # Set Zbar reader and read image
        my $image = Barcode::ZBar::Image->new();
        $image->set_format('Y800');
        $image->set_size($magick->Get(qw(columns rows)));
        $image->set_data($raw);
        my $n = $scanner->scan_image($image);
    
        # Read QR Data
        foreach my $symbol ($image->get_symbols()) {
            ($data) = $symbol->get_data() =~/data=(.*)/;
        }
        undef($image);
    
        # URL Decode
        $data =~ s/\%([A-Fa-f0-9]{2})/pack('C', hex($1))/seg;
    
        # Decode Base64 Info
        my $mime_data = decode_base64($data);
        
        # Process Protocol Buffers from de MIME Base64 Data
        my $protocol_buffer = GA->decode("$mime_data");
        foreach my $ref (@{$protocol_buffer->{Index}}) {
    
            # convert the passwords in octal notation
            $ref->{pass} =~ s/[\N{U+0000}-\N{U+FFFF}]/sprintf("\\%03o",ord($&))/eg;
    
            # Assign values to the key ring
            if ($ref->{issuer} ne '') {
                $key_ring{$ref->{issuer}}{secret} = $ref->{pass};
                $key_ring{$ref->{issuer}}{keyid}  = $ref->{keyid};
            }
            else {
                $key_ring{$ref->{keyid}}{secret} = $ref->{pass};
                $key_ring{$ref->{keyid}}{keyid}  = $ref->{keyid};
            }
        }
    }
 
    # Create and write a conf file called "ga_cli.conf"
    open(CONF, ">:encoding(UTF-8)","ga_cli.conf") or die "Can't create conf file: $!";
    print CONF "(\n";
    foreach my $issuer (sort { "\U$a" cmp "\U$b" } keys %key_ring) {
        print CONF "    '$issuer' => {\n";
        print CONF "        keyid => '$key_ring{$issuer}{keyid}',\n";
        print CONF "        secret => \"$key_ring{$issuer}{secret}\" },\n";
    }
    print CONF ");\n";
    close(CONF);
}
# If you don't give any file, print help
else {
    print "Usage:\n";
    print "    ./qr_to_ga_cli.pl \[image_file(.png|.jpg)\]\n";
}
